buildscript {
    ext {
        gdxVersion = '1.9.2'

        junitVersion = '4.11'
        mockitoVersion = '1.9.5'

        nexusStagingPluginVersion = '0.5.3'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$nexusStagingPluginVersion"
    }
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.github.ykrasik"
}

subprojects {
    apply plugin: "java"
    apply from: 'https://raw.githubusercontent.com/ykrasik/gradle-scripts/master/publish.gradle'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
    }

    // Include all sources in the jar by default, for GWT.
    // Since we have a few jars, requiring the user to explicitly depend on the sources of all these jars was uncomfortable.
    jar {
        from sourceSets.main.allJava
    }


    configurations {
        testArtifacts {
            extendsFrom testRuntime
        }
    }

    task testsJar(type: Jar, dependsOn: testClasses) {
        from sourceSets.test.output
        classifier = 'tests'
    }

    artifacts {
        testArtifacts testsJar
    }
}